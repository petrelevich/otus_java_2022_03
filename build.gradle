plugins {
    id 'idea'
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id 'org.springframework.boot' version '2.7.1' apply false
    id 'com.github.johnrengelman.shadow' version '7.1.2' apply false
    id 'com.google.cloud.tools.jib' version '3.2.1' apply false
    id 'io.freefair.lombok' version '6.4.2' apply false
}

idea {
    project {
        languageLevel = 17
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

allprojects {
    group "ru.otus"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        dependencies {
            imports {
                mavenBom "org.springframework.boot:spring-boot-dependencies:2.7.1"
                mavenBom "org.testcontainers:testcontainers-bom:1.17.1"
                mavenBom "com.google.protobuf:protobuf-bom:3.21.1"
            }
            dependency "com.google.guava:guava:${Versions.guava}"
            dependency "org.openjdk.jmh:jmh-core:${Versions.jmh}"
            dependency "org.openjdk.jmh:jmh-generator-annprocess:${Versions.jmh}"
            dependency "org.ow2.asm:asm-commons:${Versions.asm}"
            dependency("org.glassfish:jakarta.json:${Versions.glassfishJson}")
            dependency("org.webjars:sockjs-client:${Versions.sockjs}")
            dependency("org.webjars:stomp-websocket:${Versions.stomp}")
            dependency("org.webjars:bootstrap:${Versions.bootstrap}")
            dependency("org.springdoc:springdoc-openapi-ui:${Versions.springDocOpenapiUi}")

            dependency("com.google.code.findbugs:jsr305:${Versions.jsr305}")

            dependency("io.grpc:grpc-netty:${Versions.grpc}")
            dependency("io.grpc:grpc-protobuf:${Versions.grpc}")
            dependency("io.grpc:grpc-stub:${Versions.grpc}")

        }
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()

            force("javax.servlet:servlet-api:2.4")
            force("commons-logging:commons-logging:1.1.1")
            force("commons-lang:commons-lang:2.5")
            force("org.codehaus.jackson:jackson-core-asl:1.8.8")
            force("org.codehaus.jackson:jackson-mapper-asl:1.8.3")
            force("org.codehaus.jettison:jettison:1.1")
            force("net.java.dev.jna:jna:5.8.0")
            force("com.google.errorprone:error_prone_annotations:2.7.1")
            force("org.ow2.asm:asm:9.3")
        }
    }
}

//print versions from BOM
task managedVersions {
    doLast {
        dependencyManagement.managedVersions.each {
            println it
        }
    }
}

//gradle -q hello
task hello {
    println 'Hello world!'
}
